// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU.  Computes a pre-defined set of functions out = f(x,y)
 * where x and y are two 16-bit inputs. The function f is selected 
 * by a set of 6 control bits denoted zx, nx, zy, ny, f, no.
 * The ALU operation can be described using the following pseudocode:
 *     if zx=1 set x = 0       // 16-bit zero constant
 *     if nx=1 set x = !x      // Bit-wise negation
 *     if zy=1 set y = 0       // 16-bit zero constant
 *     if ny=1 set y = !y      // Bit-wise negation
 *     if f=1  set out = x + y // Integer 2's complement addition
 *     else    set out = x & y // Bit-wise And
 *     if no=1 set out = !out  // Bit-wise negation
 *
 * In addition to computing out, the ALU computes two 1-bit outputs:
 *     if out=0 set zr = 1 else zr = 0 // 16-bit equality comparison
 *     if out<0 set ng = 1 else ng = 0 // 2's complement comparison
 */

CHIP ALU {

    IN  // 16-bit inputs:
        x[16], y[16],
        // Control bits:
        zx, // Zero the x input
        nx, // Negate the x input
        zy, // Zero the y input
        ny, // Negate the y input
        f,  // Function code: 1 for add, 0 for and
        no; // Negate the out output

    OUT // 16-bit output
        out[16],              
        
        // ALU output flags
        zr, // 1 if out=0, 0 otherwise
        ng; // 1 if out<0, 0 otherwise

    PARTS:
    Not16(in=x, out=NotX);
    Mux4Way16(a=x, b=false, c=NotX, d=true, sel[0]=zx, sel[1]=nx, out=WorkingX);
    Not16(in=y, out=NotY);
    Mux4Way16(a=y, b=false, c=NotY, d=true, sel[0]=zy, sel[1]=ny, out=WorkingY);

    Add16(a=WorkingX, b=WorkingY,out=AddResult);
    And16(a=WorkingX, b=WorkingY, out=AndResult);
        
    Mux16(a=AndResult, b=AddResult, sel=f, out=CalcResult);
            
    Not16(in=CalcResult, out=NotCalcResult);
    
    // get the final output value
    Mux16(a=CalcResult, b=NotCalcResult, sel=no, out[0..7]=outBitsLSB, out[8..14]=outBitsMSB, out[15]=Neg);         
    // recreate the final output value
    And16(a[0..7]=outBitsLSB, a[8..14]=outBitsMSB, a[15]=Neg, b=true, out=out);
    
    // calculate zr
    Or8Way(in[0..6]=outBitsMSB, in[7]=Neg, out=trueInMSB);    
    Or8Way(in=outBitsLSB, out=trueInLSB);    
    Or(a=trueInMSB, b=trueInLSB, out=notZr);
    Not(in=notZr, out=zr);    
    
    // obtain 'ng'
    Or(a=Neg, b=false, out=ng);
}
